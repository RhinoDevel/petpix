2990 REM *** CONSTANTS ***
3000 A=1024:REM VIDEO RAM ADDRESS
3010 H=25:W=40:REM CHARACTER DIMENSIONS
3015 REM *** VARIABLES ***
3020 B=0:REM BUFFER FOR SCREEN CODE
3030 Y=0:X=0:REM CURSOR POSITION
3032 A$="":REM TO HOLD USER KEY INPUT
3035 REM ******************************
3040 REM *MAIN                        *
3050 REM ******************************
4000 PRINT"{clear}";
4010 GOSUB 7300:REM DRAW CURSOR
5000 GET A$:IF A$="" THEN 5000:REM WAIT
5010 GOSUB 7110:REM HANDLE KEY PRESS
5020 GOTO 5000:REM LOOP TO WAIT AGAIN
7000 POKE A+Y*W+X,C:RETURN:REM DRAW C
7010 C=B:GOSUB7000:REM REMOVE CURSOR
7015 X=X+1:IF X=W THEN X=0
7020 GOTO7300
7030 C=B:GOSUB7000
7035 Y=Y+1:IF Y=H THEN Y=0
7040 GOTO7300
7050 C=B:GOSUB7000
7055 X=X-1:IF X=-1 THEN X=W-1
7060 GOTO7300
7070 C=B:GOSUB7000
7075 Y=Y-1:IF Y=-1 THEN Y=H-1
7080 GOTO7300
7090 REM ******************************
7095 REM *HANDLE USER KEY PRESS       *
7098 REM ******************************
7100 REM *** MOVEMENT ***
7110 IF A$="{right}" THEN GOTO7010
7130 IF A$="{down}" THEN GOTO7030
7150 IF A$="{left}" THEN GOTO7050
7170 IF A$="{up}" THEN GOTO7070
7180 REM *** DRAWING ***
7190 IF A$="q" THEN GOTO 8010
7290 RETURN
7292 REM ******************************
7295 REM *SAVE CHAR.AT CUR.POS.INTO B *
7296 REM *AND DRAW CURSOR AT CUR.POS. *
7298 REM ******************************
7300 B=PEEK(A+Y*W+X):REM SAVE CHAR.
7302 REM ******************************
7305 REM *JUST DRAW CURSOR AT CUR.POS.*
7306 REM *BY INVERTING SCREEN CODE    *
7307 REM *STORED IN VARIABLE B        *
7308 REM ******************************
7310 C=(B OR 128)-(B AND 128):REM INV.
7315 GOSUB 7000:REM DRAW CURSOR
7320 RETURN
8010 B=60:REM TEST
8020 GOTO 7310
